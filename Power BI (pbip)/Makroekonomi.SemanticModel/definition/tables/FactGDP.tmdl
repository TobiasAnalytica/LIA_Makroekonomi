table FactGDP
	lineageTag: e35dd26f-e597-4fc0-9c02-5d2d7c69fe8b

	measure 'GDP – Value (Quarterly)' =
			
			SUM ( FactGDP[GDPAmount] )
			// Värdet i aktuell kontext (kvartalsnivå)
		displayFolder: DAX-GDP\Base
		lineageTag: c768d703-d155-4e38-990a-d9a79d27db74

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Latest Date – GDP (PerEntity)' = ```
			
			VAR d =
			    CALCULATE (
			        MAX ( DimDate[Date] ),
			        FILTER (
			            ALLSELECTED ( DimDate[Date] ),
			            NOT ISBLANK ( [GDP – Value (Quarterly)] )
			        )
			    )
			RETURN d
			
			```
		formatString: General Date
		displayFolder: DAX-GDP\Dates
		lineageTag: 6ea76560-128d-42ea-bf49-0076b865527e

	measure 'Latest Date Label – GDP' = ```
			
			FORMAT ( [Latest Date – GDP (PerEntity)], "yyyy-Q" )
			
			```
		displayFolder: DAX-GDP\Dates
		lineageTag: e15117c0-05b2-4bd2-a3d5-464c306a29dd

	measure 'GDP – Value (EOP,Quarter)' =
			
			// End-of-period givet vald period
			CALCULATE (
			    [GDP – Value (Quarterly)],
			    LASTNONBLANK ( DimDate[Date], [GDP – Value (Quarterly)] )
			)
		displayFolder: DAX-GDP\Base
		lineageTag: cab074a3-70f6-4f13-96b2-c3746d648d14

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'GDP – QoQ Δ (Value)' =
			
			VAR Prev =
			    CALCULATE ( [GDP – Value (EOP,Quarter)], DATEADD ( DimDate[Date], -1, QUARTER ) )
			RETURN [GDP – Value (EOP,Quarter)] - Prev
		displayFolder: DAX-GDP\Base
		lineageTag: 17c888fa-027f-4660-a99a-4852444ba4f5

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'GDP – QoQ Δ %' =
			
			VAR Prev =
			    CALCULATE ( [GDP – Value (EOP,Quarter)], DATEADD ( DimDate[Date], -1, QUARTER ) )
			RETURN DIVIDE ( [GDP – Value (EOP,Quarter)] - Prev, Prev )
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Base
		lineageTag: ce507cff-328e-4c33-be9e-05f4692228c6

	measure 'GDP – YoY Δ (Value)' =
			
			VAR Prev =
			    CALCULATE ( [GDP – Value (EOP,Quarter)], DATEADD ( DimDate[Date], -1, YEAR ) )
			RETURN [GDP – Value (EOP,Quarter)] - Prev
		displayFolder: DAX-GDP\Base
		lineageTag: d1fce9e8-69d7-4d11-8424-2fe9f84cdde9

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'GDP – YoY Δ % (Quarterly)' =
			
			VAR Prev =
			    CALCULATE ( [GDP – Value (EOP,Quarter)], DATEADD ( DimDate[Date], -1, YEAR ) )
			RETURN DIVIDE ( [GDP – Value (EOP,Quarter)] - Prev, Prev )
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Base
		lineageTag: 193d7170-f859-46e1-9519-a748b71ee9d5

	measure 'GDP – 4Q Sum (At Date)' =
			
			SUMX (
			    DATESINPERIOD ( DimDate[Date], MAX ( DimDate[Date] ), -12, MONTH ),
			    [GDP – Value (EOP,Quarter)]
			)
			// “4Q Rolling Sum” = ”senaste 4 kvartal” ⇢ robust “års-BNP” oavsett om året är komplett
		formatString: 0
		displayFolder: DAX-GDP\Base
		lineageTag: 654450b0-0218-4afc-a831-d2a4cc582cda

	measure 'GDP – Value (Annual, In Year Context)' =
			
			VAR Y =
			    SELECTEDVALUE ( DimDate[Year] )
			RETURN
			    IF (
			        ISBLANK ( Y ),
			        BLANK(),
			        CALCULATE ( [GDP – Value (Quarterly)], KEEPFILTERS ( VALUES ( DimDate[YearQuarter] ) ) )
			    )
			// Årsvärde i ren årskontext (t.ex. staplar per år i diagram)
		formatString: 0
		displayFolder: DAX-GDP\Base
		lineageTag: 33c93897-9a8c-4c67-a566-71d0b503a8b3

	measure 'GDP – YoY Δ % (4Q Sum)' =
			
			VAR Prev =
			    CALCULATE ( [GDP – 4Q Sum (At Date)], DATEADD ( DimDate[Date], -1, YEAR ) )
			RETURN DIVIDE ( [GDP – 4Q Sum (At Date)] - Prev, Prev )
			// Tillväxt på “års-BNP” (4Q-summa) – KPI
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Base
		lineageTag: 9f03b543-5847-4a2f-8eaf-a3ae6de14bc1

	measure 'GDP – CAGR % (5Y, 4Q Sum)' =
			
			VAR EndVal  = [GDP – 4Q Sum (At Date)]
			VAR StartVal =
			    CALCULATE ( [GDP – 4Q Sum (At Date)], DATEADD ( DimDate[Date], -5, YEAR ) )
			RETURN IF ( StartVal > 0, ( EndVal / StartVal ) ^ ( 1 / 5 ) - 1 )
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Base
		lineageTag: d8f7eea9-84dc-4db7-b700-872fa4a5dc64

	measure 'GDP – Per Capita (4Q Sum, EUR/Person)' =
			
			DIVIDE ( [GDP – 4Q Sum (At Date)], [Population – Persons (EOP,Annual)] )
		displayFolder: DAX-GDP\Base
		lineageTag: 2f03de7d-b2a9-49dd-9b8b-f28870d2c83a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'GDP – Per Capita YoY Δ % (4Q Sum)' =
			
			VAR Prev =
			    CALCULATE ( [GDP – Per Capita (4Q Sum, EUR/Person)], DATEADD ( DimDate[Date], -1, YEAR ) )
			RETURN DIVIDE ( [GDP – Per Capita (4Q Sum, EUR/Person)] - Prev, Prev )
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Base
		lineageTag: 9d042963-761b-455d-96ad-78b9d2ef8d61

	measure 'Latest Date – Common year(GDPpc+Unemp+Pop)' = ```
			
			VAR Regions = VALUES( DimRegion[Description] )
			RETURN
			MINX(
			    Regions,
			    VAR d_g = [LastDate – GDPpc (Per Region)]
			    VAR d_u = [LastDate – Unemp (Per Region)]
			    VAR d_p = [LastDate – Population (Per Region)]
			    /* Filtrera bort tomma värden och ta min av de som finns */
			    VAR vals =
			        FILTER( { d_g, d_u, d_p }, NOT ISBLANK( [Value] ) )
			    RETURN MINX( vals, [Value] )
			)
			
			```
		formatString: yyyy
		displayFolder: DAX-GDP\Dates
		lineageTag: 8c34f979-40f7-4211-ada9-198aa7299661

		annotation PBI_FormatHint = {"isDateTimeCustom":true}

	measure 'GDP – Per Capita (Latest Common)' =
			
			VAR d = [Latest Date – Common year(GDPpc+Unemp+Pop)]
			RETURN
			    CALCULATE( [GDP – Per Capita (4Q Sum, EUR/Person)], TREATAS( { d }, DimDate[Date] ) )
		displayFolder: DAX-GDP\Base
		lineageTag: f701827e-70c8-450d-8c0a-12ba8747a8a1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Unemployment – Rate % (EOP, Latest Common)' =
			
			VAR d = [Latest Date – Common year(GDPpc+Unemp+Pop)]
			RETURN
			    CALCULATE( [Unemployment – Rate % (EOP)], TREATAS( { d }, DimDate[Date] ) )
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Base
		lineageTag: 9c743a7d-5310-4bdf-9e64-86e2981e3b18

	measure 'Bubble Size – Population (Latest Common)' = ```
			
			VAR d = [Latest Date – Common year(GDPpc+Unemp+Pop)]
			RETURN
			    CALCULATE ( [Population – Persons (EOP,Annual)], TREATAS ( { d }, DimDate[Date] ) )
			
			```
		displayFolder: DAX-GDP\Scatter
		lineageTag: 8c169bb7-83b9-4883-bfe7-1812aab2dd38

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'LastDate – GDP 4Q (Per Region)' =
			
			CALCULATE (
			    MAX ( DimDate[Date] ),
			    FILTER ( ALLSELECTED ( DimDate[Date] ), NOT ISBLANK ( [GDP – 4Q Sum (At Date)] ) )
			)
			// GDP 4Q-summa: senaste datum med värde
		formatString: General Date
		displayFolder: DAX-GDP\Dates
		lineageTag: cd798925-9d10-4029-b811-277673ce6b3e

	measure 'LastDate – Population (Per Region)' =
			
			CALCULATE (
			    MAX ( DimDate[Date] ),
			    FILTER ( ALLSELECTED ( DimDate[Date] ), NOT ISBLANK ( [Population – Persons (EOP,Annual)] ) )
			)
			// Population EOP: senaste datum med värde
		formatString: General Date
		displayFolder: DAX-GDP\Dates
		lineageTag: d031116e-e8cf-4bbf-ae87-5e92ee0b843e

	measure 'LastDate – GDPpc (Per Region)' = ```
			
			MIN ( [LastDate – GDP 4Q (Per Region)], [LastDate – Population (Per Region)] )
			
			```
		formatString: General Date
		displayFolder: DAX-GDP\Dates
		lineageTag: e290ff45-3b3e-47b8-8b5d-88139efbd55f

	measure 'Title – Scatter: GDPpc vs Unemployment (Latest Common Year)' = ```
			
			VAR d = [Latest Date – Common year(GDPpc+Unemp+Pop)]
			VAR yearLbl =
			    IF ( ISBLANK ( d ), "—", FORMAT ( d, "yyyy" ) )
			VAR nRegions = COUNTROWS ( ALLSELECTED ( DimRegion[Description] ) )
			RETURN
			"BNP per capita (4Q) vs Arbetslöshet (år: " & yearLbl & ") — " &
			"Regioner i urval: " & nRegions
			
			```
		displayFolder: DAX-GDP\Scatter
		lineageTag: 02633f2a-3371-40d2-a1d6-38cd1df3d0a6

	measure 'Subtitle – Scatter: GDPpc vs Unemployment' = ```
			
			"X: BNP/capita som rullande 4 kvartal (€)  |  " &
			"Y: Arbetslöshetsgrad (EOP, %)  |  " &
			"Storlek: Befolkning (EOP, årlig)"
			
			```
		displayFolder: DAX-GDP\Scatter
		lineageTag: 1fd4a16d-567d-47bb-974c-2af72e7f541a

	measure 'Play – Year End (label)' = ```
			
			VAR y = SELECTEDVALUE( DimDate[Year] )
			RETURN IF( ISBLANK(y), BLANK(), FORMAT( DATE(y,12,31), "yyyy" ) )
			
			```
		displayFolder: DAX-GDP\Play axis
		lineageTag: 69fbb68f-4589-400c-a4a3-a1482a686f9a

	measure 'GDP – Per Capita (At Play Year)' = ```
			
			VAR y = SELECTEDVALUE( DimDate[Year] )
			RETURN
			IF(
			    ISBLANK(y), BLANK(),
			    CALCULATE(
			        [GDP – Per Capita (4Q Sum, EUR/Person)],
			        DimDate[Year] = y
			    )
			)
			
			```
		displayFolder: DAX-GDP\Play axis
		lineageTag: 7e615516-7d4a-4dcf-a071-25dfe190651c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Unemployment – Rate % (EOP, At Play Year)' = ```
			
			VAR y = SELECTEDVALUE( DimDate[Year] )
			RETURN
			IF(
			    ISBLANK(y), BLANK(),
			    CALCULATE(
			        [Unemployment – Rate % (EOP)],
			        DimDate[Year] = y
			    )
			)
			
			```
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Play axis
		lineageTag: 484307ba-9c6b-490c-b6f9-7db2b92dc8c1

	measure 'Bubble Size – Population (At Play Year)' = ```
			
			VAR y = SELECTEDVALUE( DimDate[Year] )
			RETURN
			IF(
			    ISBLANK(y), BLANK(),
			    CALCULATE(
			        [Population – Persons (EOP,Annual)],
			        DimDate[Year] = y
			    )
			)
			
			```
		formatString: 0
		displayFolder: DAX-GDP\Play axis
		lineageTag: f4f0a40c-dab2-4e9f-b813-4939094b4a1c

	measure 'GDP – 4Q Sum (Latest)' =
			
			VAR d = [Latest Date – GDP (PerEntity)]
			RETURN
			    IF ( ISBLANK(d), BLANK(),
			        CALCULATE ( [GDP – 4Q Sum (At Date)], TREATAS ( { d }, DimDate[Date] ) )
			    )
		formatString: 0
		displayFolder: DAX-GDP\Ranking
		lineageTag: f9c03c0a-86cb-40fb-a44c-6b60766bf146

	measure 'GDP – 4Q Sum (Latest-1Y)' =
			
			VAR d  = [Latest Date – GDP (PerEntity)]
			VAR d0 = IF ( ISBLANK(d), BLANK(), EDATE ( d, -12 ) )
			RETURN
			    IF ( ISBLANK(d0), BLANK(),
			        CALCULATE ( [GDP – 4Q Sum (At Date)], TREATAS ( { d0 }, DimDate[Date] ) )
			    )
		formatString: 0
		displayFolder: DAX-GDP\Ranking
		lineageTag: 9bd9d4a2-3aef-41c3-aac9-8eac6d70eae8

	measure 'GDP – YoY Δ % (4Q Sum at Latest)' =
			
			VAR curr = [GDP – 4Q Sum (Latest)]
			VAR prev = [GDP – 4Q Sum (Latest-1Y)]
			RETURN DIVIDE ( curr - prev, prev )
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Ranking
		lineageTag: af588aab-c9fc-4814-8c3b-1a98bb9b1370

	measure 'Rank – GDP (4Q Sum, Latest)' =
			
			RANKX ( ALLSELECTED ( DimRegion[Description] ), [GDP – 4Q Sum (Latest)], , DESC, DENSE )
		formatString: 0
		displayFolder: DAX-GDP\Ranking
		lineageTag: bc0807fb-ab55-40ce-94c8-5e3af7f589ba

	measure 'Latest Date – GDP (Common)' =
			
			VAR nRegions = [Regions – Selected (Count)]
			VAR datesWithAll =
			    FILTER (
			        ALLSELECTED ( DimDate[Date] ),
			        NOT ISBLANK ( [GDP – Value (Quarterly)] ) &&
			        CALCULATE ( DISTINCTCOUNT ( FactGDP[RegionKey] ) ) = nRegions
			    )
			RETURN MAXX ( datesWithAll, DimDate[Date] )
			// Senaste datum (kvartal) där ALLA valda regioner har BNP-värde
		formatString: General Date
		displayFolder: DAX-GDP\Dates
		lineageTag: 99d7a3e5-30e6-4abc-b510-62cba67600de

	measure 'Latest Date Label – GDP (Common)' = ```
			
			VAR d = [Latest Date – GDP (Common)]
			RETURN IF ( ISBLANK ( d ), BLANK(), FORMAT ( d, "yyyy-\QQ" ) )
			
			```
		displayFolder: DAX-GDP\Dates
		lineageTag: a608ac4c-1ad0-46e7-8ea9-b2e574c43487

	measure 'GDP – Value (EOP, Latest Common)' =
			
			VAR d = [Latest Date – GDP (Common)]
			RETURN
			    IF (
			        ISBLANK ( d ),
			        BLANK(),
			        CALCULATE ( [GDP – Value (EOP,Quarter)], TREATAS ( { d }, DimDate[Date] ) )
			    )
			    // Kvartalsnivå vid latest common
		formatString: 0
		displayFolder: DAX-GDP\Base
		lineageTag: 5e59d568-360e-4e5a-bc25-2942c67fd325

	measure 'GDP – 4Q Sum (At Latest Common)' =
			
			VAR d = [Latest Date – GDP (Common)]
			RETURN
			    IF (
			        ISBLANK ( d ),
			        BLANK(),
			        CALCULATE ( [GDP – 4Q Sum (At Date)], TREATAS ( { d }, DimDate[Date] ) )
			    )
			// Års-BNP som 4-kvartalssumma, vid latest common
		formatString: 0
		displayFolder: DAX-GDP\Base
		lineageTag: 185e5a28-876f-48f4-8482-1e0380fb9d03

	measure 'GDP – YoY Δ % (4Q Sum at Latest Common)' =
			
			VAR d    = [Latest Date – GDP (Common)]
			VAR curr = CALCULATE ( [GDP – 4Q Sum (At Date)], TREATAS ( { d }, DimDate[Date] ) )
			VAR prev = CALCULATE ( [GDP – 4Q Sum (At Date)], TREATAS ( { EDATE ( d, -12 ) }, DimDate[Date] ) )
			RETURN DIVIDE ( curr - prev, prev )
			// YoY på 4Q-summan, vid latest common (stabil jämförelse)
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Base
		lineageTag: d79beb0c-57b4-4c38-b6c7-cde8f64bc166

	measure 'GDP – QoQ Δ % (At Latest Common)' =
			
			VAR d    = [Latest Date – GDP (Common)]
			VAR curr = CALCULATE ( [GDP – Value (EOP,Quarter)], TREATAS ( { d }, DimDate[Date] ) )
			VAR prev = CALCULATE ( [GDP – Value (EOP,Quarter)], TREATAS ( { EDATE ( d, -3 ) }, DimDate[Date] ) )
			RETURN DIVIDE ( curr - prev, prev )
			//QoQ på kvartalsnivå, vid latest common
		displayFolder: DAX-GDP\Base
		lineageTag: 19fc66fd-ad82-4758-b430-21cf738d2b58

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Rank – GDP (4Q Sum, Latest Common)' = ```
			
			RANKX (
			    ALLSELECTED ( DimRegion[Description] ),
			    [GDP – 4Q Sum (At Latest Common)],
			    ,
			    DESC,
			    DENSE
			)
			
			```
		formatString: 0
		displayFolder: DAX-GDP\Ranking
		lineageTag: 307c3855-a29a-4ae0-a05c-ea6c726998f6

	measure 'Show – TopN Filter (GDP 4Q Latest Common)' = ```
			
			IF ( [Rank – GDP (4Q Sum, Latest Common)] <= [TopN – Selected], 1, 0 )
			
			```
		displayFolder: DAX-GDP\Ranking
		lineageTag: 732c0530-e23c-4a94-a0cf-57b9bf6a9e04

	measure 'TopN – Selected' =
			
			SELECTEDVALUE ( 'TopN – Values'[TopN], 10 )
		formatString: 0
		displayFolder: DAX-GDP\Ranking
		lineageTag: 2950b7dc-406f-46ed-aa0c-f9b56f9e8240

	measure 'Title – GDP Ranking (4Q Sum, Common)' = ```
			
			VAR n     = [TopN – Selected]
			VAR dLab  = [Latest Date Label – GDP (Common)]
			VAR nL    = DISTINCTCOUNT ( DimRegion[Description] )
			RETURN
			"Top " & n & " – Års-BNP (4Q-summa) – Gemensam period: " & dLab & "  |  Länder: " & nL
			
			```
		displayFolder: DAX-GDP\Ranking
		lineageTag: d3b8f6de-ea0f-4d19-862a-54977698318a

	measure 'Subtitle – GDP Ranking (4Q Sum, Common)' = ```
			
			VAR saz = SELECTEDVALUE ( DimSeasonalAdjustment[SeasonalAdjustmentAK], "SA/NSA/CA" )
			RETURN
			"Samma kvartal för alla regioner (Latest Common). Mått: 4Q-summa. Säsongsjustering: " & saz & "."
			
			```
		displayFolder: DAX-GDP\Ranking
		lineageTag: 3b3b55c4-69f6-44fa-93ce-e8ac64214acc

	measure 'Heatmap – Show (Common, Last 12Q)' = ```
			
			VAR dCell = MAX ( DimDate[Date] )
			VAR dEnd  = [Latest Date – GDP (Common)]
			VAR inWindow = dCell <= dEnd && dCell > EDATE ( dEnd, -36 )   -- 12 kvartal = 36 månader
			RETURN IF ( inWindow && [Is Common Quarter – GDP] = 1, 1, 0 )
			
			```
		formatString: 0
		displayFolder: DAX-GDP\Heatmap
		lineageTag: 2f93f50a-0495-4898-a395-01f2206d9b55

	measure 'Title – GDP Heatmap (YoY %, Common, 12Q)' = ```
			
			VAR dLab = [Latest Date Label – GDP (Common)]
			VAR nL   = DISTINCTCOUNT ( DimRegion[Description] )
			RETURN
			"Heatmap – BNP YoY %  |  Gemensam period till: " & dLab & "  |  Länder: " & nL & "  |  Fönster: 12 kvartal"
			
			```
		displayFolder: DAX-GDP\Heatmap
		lineageTag: 950ed2c1-6078-4ed9-9d41-8b02090dc2bc

	measure 'Subtitle – GDP Heatmap (YoY %, Common)' = ```
			
			VAR saz = SELECTEDVALUE ( DimSeasonalAdjustment[SeasonalAdjustmentAK], "SA/NSA/CA" )
			RETURN
			"Rader = land, kolumner = kvartal. Färg = BNP YoY %. Center = 0%. Säsongsjustering: " & saz & "."
			
			```
		displayFolder: DAX-GDP\Heatmap
		lineageTag: a12262fe-0409-4016-b979-087705cb0b10

	measure 'Is Common Quarter – GDP' = ```
			
			VAR nRegions = [Regions – Selected (Count)]
			RETURN
			IF (
			    CALCULATE ( DISTINCTCOUNT ( FactGDP[RegionKey] ) ) = nRegions
			        && NOT ISBLANK ( [GDP – Value (Quarterly)] ),
			    1, 0
			)
			
			```
		formatString: 0
		displayFolder: DAX-GDP\Heatmap
		lineageTag: c3b706e1-f1f8-4d5d-8238-6a04839f0079

	measure 'View – Selected (GDP)' = ```
			
			SELECTEDVALUE ( 'View – GDP Options'[ViewKey], 1 )   // 1=Kvartal (default)
			
			```
		formatString: 0
		displayFolder: DAX-GDP\Switch
		lineageTag: 5e78d288-3043-4b19-bebe-b5449e00e314

	measure 'GDP – Bar (Selected View)' = ```
			
			SWITCH(
			    [View – Selected (GDP)],
			    1, [GDP – Value (Quarterly)],          -- Kvartal (summeras per år på Year-axel)
			    2, [GDP – 4Q Sum (At Date)]            -- Rullande år (4 kvartal)
			)
			
			```
		formatString: 0
		displayFolder: DAX-GDP\Switch
		lineageTag: a82f70ad-5eca-47aa-9799-0ac0841269ca

	measure 'GDP – Line % (Selected View)' = ```
			
			SWITCH(
			    [View – Selected (GDP)],
			    1, [GDP – YoY Δ % (Quarterly)],        -- YoY% på kvartalsnivå
			    2, [GDP – YoY Δ % (4Q Sum)]            -- YoY% på 4Q-summan
			)
			
			```
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: DAX-GDP\Switch
		lineageTag: 85cf2fc4-2854-41ee-89a7-5c2454d599d5

	measure 'Title – GDP Trend (Selected View, Common)' = ```
			
			VAR v     = SELECTEDVALUE ( 'View – GDP Options'[ViewLabel], "Kvartal" )
			VAR dLab  = [Latest Date Label – GDP (Common)]
			VAR nL    = DISTINCTCOUNT ( DimRegion[Description] )
			RETURN
			"Trend – BNP  |  Vy: " & v
			& "  |  Gemensam period till: " & dLab
			& "  |  Länder: " & nL
			
			```
		displayFolder: DAX-GDP\Switch
		lineageTag: 27863e54-c42d-4c2f-aa9e-7cb74c6086db

	measure 'Subtitle – GDP Trend (Selected View)' = ```
			
			VAR saz = SELECTEDVALUE ( DimSeasonalAdjustment[SeasonalAdjustmentAK], "SA/NSA/CA" )
			RETURN
			IF(
			    [View – Selected (GDP)] = 1,
			    "Kolumner: BNP per kvartal (summeras per år på Year-axel). Linje: BNP YoY % (kvartal). Säsongsjustering: " & saz & ".",
			    "Kolumner: BNP 4Q-summa (rullande år). Linje: BNP YoY % (4Q-summa). Säsongsjustering: " & saz & "."
			)
			
			```
		displayFolder: DAX-GDP\Switch
		lineageTag: 4a1495e3-ed6c-4327-9aae-0c0838e76b3e

	column FactGDPKey
		dataType: int64
		formatString: 0
		lineageTag: 886aee07-3e00-4895-9967-3910594df708
		summarizeBy: count
		sourceColumn: FactGDPKey

		annotation SummarizationSetBy = Automatic

	column DateKey
		dataType: int64
		formatString: 0
		lineageTag: 38b54f59-561f-410a-8f5d-3f6ae2374f6b
		summarizeBy: none
		sourceColumn: DateKey

		annotation SummarizationSetBy = Automatic

	column TimeFrequencyKey
		dataType: int64
		formatString: 0
		lineageTag: 0b8cb8e4-8984-4034-bf46-c8fd192c2bbd
		summarizeBy: none
		sourceColumn: TimeFrequencyKey

		annotation SummarizationSetBy = Automatic

	column SeasonalAdjustmentKey
		dataType: int64
		formatString: 0
		lineageTag: 165db36d-f23d-4adb-a7eb-f581cb23e946
		summarizeBy: none
		sourceColumn: SeasonalAdjustmentKey

		annotation SummarizationSetBy = Automatic

	column RegionKey
		dataType: int64
		formatString: 0
		lineageTag: 34e4d809-67fc-4375-a63e-3f172f0a2684
		summarizeBy: none
		sourceColumn: RegionKey

		annotation SummarizationSetBy = Automatic

	column FlagKey
		dataType: int64
		formatString: 0
		lineageTag: 86d98a8a-ea0f-477f-a0ec-bc7473ad543b
		summarizeBy: none
		sourceColumn: FlagKey

		annotation SummarizationSetBy = Automatic

	column GDPAmount
		dataType: int64
		formatString: 0
		lineageTag: 4b718112-9988-4fb1-99a6-5219d76415dd
		summarizeBy: sum
		sourceColumn: GDPAmount

		annotation SummarizationSetBy = Automatic

	column CreatedGoldAt
		dataType: dateTime
		formatString: General Date
		lineageTag: c97dd408-5cb1-4e3d-aa5b-4423581babef
		summarizeBy: none
		sourceColumn: CreatedGoldAt

		variation Variation
			isDefault
			relationship: 211bfd71-ebe6-4c37-b0bc-f68a59d9454d
			defaultHierarchy: LocalDateTable_2fdb79d6-a9fd-4cc7-a279-f7323dd9688c.'Date Hierarchy'

		annotation SummarizationSetBy = Automatic

	partition FactGDP = m
		mode: import
		source =
				let
				    Source = Sql.Databases("."),
				    LIA_DW_Gold = Source{[Name="LIA_DW_Gold"]}[Data],
				    dbo_FactGDP = LIA_DW_Gold{[Schema="dbo",Item="FactGDP"]}[Data]
				in
				    dbo_FactGDP

	annotation PBI_NavigationStepName = Navigation

	annotation PBI_ResultType = Table

